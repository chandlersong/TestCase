plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'groovy'
    id 'org.springframework.boot'
    id 'org.springframework.cloud.contract' version "${contractPluginVersion}"
    id 'maven-publish'
}

group 'org.example'
version '1.0-SNAPSHOT'


println "Boot Version [${bootVersion}], Cloud version [${BOM_VERSION}], Contract version [${verifierVersion}]"

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation "org.springframework.boot:spring-boot-starter-web"
    testImplementation "org.springframework.cloud:spring-cloud-starter-contract-verifier"
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}


contracts {
    testFramework = "JUNIT5"
    baseClassForTests="org.example.producer.BaseTestClass"
}

contractTest {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact bootJar

            artifact verifierStubsJar

            // https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/273
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
        }
    }
}



